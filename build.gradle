buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: "maven-publish"
apply plugin: "eclipse"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

version = "${mcforge_version}-${mod_version}${getClassifier()}"
group = "com.terraforged"
archivesBaseName = "terraforged"

allprojects {
    apply plugin: "java"
}

configurations {
    lib
    compile.extendsFrom lib
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://io.terraforged.com/repository/maven/" }
}

dependencies {
    lib(project(":FeatureManager")) { transitive false }
//    lib(project(":Engine"))
    lib "com.terraforged:Engine:0.1.0"
}

minecraft {
    mappings = mcp_channel_with_version
    runDir = "run"
    version = mcforge_version
}

task collectLibs(type: Copy) {
    // require upstream projects to be jar'd first
    dependsOn(dependsOn: subprojects.tasks["jar"])

    // copy contents to build dir ignoring dupes
    from { configurations.lib.collect { it.isDirectory() ? it : zipTree(it) } }
    into("build/classes/java/main")
    duplicatesStrategy = "EXCLUDE"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

processResources {

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand "version": "${mod_version}${getBuildNumber()}", "mc_version": mcforge_version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

classes {
    // collect all dependency classes first
    dependsOn(collectLibs)
}

build {
//    dependsOn("reobfJar")
}

publishing {
    publications {
        // publish the mod jar & api jar
        mavenJava(MavenPublication) {
            artifact jar
            artifact sourcesJar
        }
    }

    if (System.getenv("MAVEN_USER") != null && System.getenv("MAVEN_PASS") != null) {
        repositories {
            maven {
                url = "https://io.terraforged.com/repository/maven/"
                credentials {
                    username System.getenv("MAVEN_USER")
                    password System.getenv("MAVEN_PASS")
                }
            }
        }
    }
}

static def getClassifier() {
    // not snapshot if building a release jar
    if (System.getenv("GIT_TAG_NAME") != null) {
        return ""
    }
    return "-SNAPSHOT"
}

static def getBuildNumber() {
    // ignore build number if building a release jar
    if (System.getenv("GIT_TAG_NAME") != null) {
        return ""
    }
    // append build number on mod manifest
    def buildNumber = System.getenv("BUILD_NUMBER")
    if (buildNumber != null) {
        return "-${buildNumber}"
    }
    return ""
}